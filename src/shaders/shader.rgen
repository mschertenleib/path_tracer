#version 460

#extension GL_EXT_ray_tracing: require
#extension GL_EXT_scalar_block_layout: require
#extension GL_EXT_debug_printf: enable

#include "shader_common.glsl"

layout (binding = 0, rgba32f) uniform restrict image2D storage_image;

layout (binding = 1) uniform accelerationStructureEXT tlas;

layout (binding = 2, scalar) restrict readonly buffer Vertices
{
    vec3 vertices[];
};

layout (binding = 3, scalar) restrict readonly buffer Indices
{
    uint indices[];
};

layout (binding = 4, rgba8) uniform restrict writeonly image2D render_target;

layout (push_constant, scalar) uniform Push_constants
{
    uint global_frame_count;
    uint sample_count;
    uint samples_per_frame;
    vec3 camera_position;
    vec3 camera_dir_x;
    vec3 camera_dir_y;
    vec3 camera_dir_z;
    float sensor_distance;
    float sensor_half_width;
    float sensor_half_height;
    float focus_distance;
    float aperture_radius;
} push;

layout (location = 0) rayPayloadEXT Ray_payload payload;


uint hash(uint x)
{
    x += x << 10;
    x ^= x >> 6;
    x += x << 3;
    x ^= x >> 11;
    x += x << 15;
    return x;
}

// Normal distribution with zero mean and unit standard deviation
vec2 sample_gaussian(inout uint rng_state)
{
    const float float_min = 1.175494351e-38;
    const float r = sqrt(-2.0 * log(max(float_min, random(rng_state))));
    const float theta = 2.0 * pi * random(rng_state);
    return r * vec2(cos(theta), sin(theta));
}

vec2 sample_disk(inout uint rng_state)
{
    const float r = sqrt(random(rng_state));
    const float theta = 2.0 * pi * random(rng_state);
    return r * vec2(cos(theta), sin(theta));
}

vec3 radiance(out uint bounces)
{
    vec3 accumulated_color = vec3(0.0);
    vec3 accumulated_reflectance = vec3(1.0);

    for (bounces = 0; bounces < 32; ++bounces)
    {
        traceRayEXT(tlas,
                    gl_RayFlagsOpaqueEXT,
                    0xff,
                    0,
                    0,
                    0,
                    payload.ray_origin,
                    0.0,
                    payload.ray_direction,
                    10000.0,
                    0);

        accumulated_color += accumulated_reflectance * payload.emissivity;
        if (payload.hit_sky)
        {
            return accumulated_color;
        }

        vec3 hit_color = payload.color;
        const float p = max(hit_color.r, max(hit_color.g, hit_color.b));
        if (random(payload.rng_state) < p)
        {
            hit_color /= p;
        }
        else
        {
            return accumulated_color;
        }
        accumulated_reflectance *= hit_color;
    }

    return accumulated_color;
}


// ACES tone mapping code from
// https://github.com/TheRealMJP/BakingLab/blob/master/BakingLab/ACES.hlsl,
// originally written by Stephen Hill (@self_shadow)

vec3 rrt_and_odt_fit(vec3 v)
{
    const vec3 a = v * (v + 0.0245786) - 0.000090537;
    const vec3 b = v * (0.983729 * v + 0.4329510) + 0.238081;
    return a / b;
}

vec3 ACES_tone_map(vec3 color)
{
    // sRGB => XYZ => D65_2_D60 => AP1 => RRT_SAT
    const mat3 input_mat = mat3(
        0.59719, 0.07600, 0.02840,
        0.35458, 0.90834, 0.13383,
        0.04823, 0.01566, 0.83777
    );

    // ODT_SAT => XYZ => D60_2_D65 => sRGB
    const mat3 output_mat = mat3(
        1.60475, -0.10208, -0.00327,
        -0.53108, 1.10813, -0.07276,
        -0.07367, -0.00605, 1.07602
    );

    color = input_mat * color;
    color = rrt_and_odt_fit(color);
    color = output_mat * color;

    return clamp(color, 0.0, 1.0);
}


// Khronos PBR neutral tone mapper from
// https://modelviewer.dev/examples/tone-mapping

vec3 PBR_neutral_tone_map(vec3 color)
{
    const float start_compression = 0.8 - 0.04;
    const float desaturation = 0.15;

    const float x = min(color.r, min(color.g, color.b));
    const float offset = x < 0.08 ? x - 6.25 * x * x : 0.04;
    color -= offset;

    const float peak = max(color.r, max(color.g, color.b));
    if (peak < start_compression)
    {
        return color;
    }

    const float d = 1.0 - start_compression;
    const float new_peak = 1.0 - d * d / (peak + d - start_compression);
    color *= new_peak / peak;

    const float g = 1.0 - 1.0 / (desaturation * (peak - new_peak) + 1.0);
    return mix(color, new_peak * vec3(1.0, 1.0, 1.0), g);
}


void main()
{
    const uvec2 image_size = imageSize(storage_image);

    if (gl_LaunchIDEXT.x >= image_size.x || gl_LaunchIDEXT.y >= image_size.y)
    {
        return;
    }

    const uint pixel_index = gl_LaunchIDEXT.y * image_size.x + gl_LaunchIDEXT.x;
    payload.rng_state = hash(pixel_index) + hash(push.global_frame_count + 1);

    vec4 accumulated_color = vec4(0.0);

    for (uint s = 0; s < push.samples_per_frame; ++s)
    {
        const vec2 offset = vec2(0.5) + 0.375 * sample_gaussian(payload.rng_state);
        const vec2 uv = 2.0 * (vec2(gl_LaunchIDEXT.xy) + offset) / vec2(image_size) - vec2(1.0);

        const vec2 defocus = push.aperture_radius * sample_disk(payload.rng_state);
        const vec3 defocus_offset = push.camera_dir_x * defocus.x + push.camera_dir_y * defocus.y;
        payload.ray_origin = push.camera_position + defocus_offset;
        payload.ray_direction = normalize(push.focus_distance *
            (push.camera_dir_z * push.sensor_distance
                + push.camera_dir_x * push.sensor_half_width * uv.x
                + push.camera_dir_y * push.sensor_half_height * uv.y)
            - defocus_offset);

        uint bounces;
        const vec3 color = radiance(bounces);
#if 1
        accumulated_color += vec4(color, 1.0);
#else
        accumulated_color += vec4(vec3(bounces / 16.0), 1.0);
#endif
    }

    vec4 average_color = imageLoad(storage_image, ivec2(gl_LaunchIDEXT.xy));
    // NOTE: if the image is uninitialized, it might contain NaNs which will propagate.
    // So we must explicitely handle the first sample.
    // TODO: it is probably better to clear the image to zero when creating it, and then always
    // load the old value even if we will end up multiplying it by zero on a render reset.
    if (push.sample_count > 0)
    {
        average_color = (average_color * push.sample_count + accumulated_color)
            / (push.sample_count + push.samples_per_frame);
    }
    else
    {
        average_color = accumulated_color / push.samples_per_frame;    
    }
    imageStore(storage_image, ivec2(gl_LaunchIDEXT.xy), average_color);

#if 1
    const vec3 render_color = PBR_neutral_tone_map(average_color.rgb);
#else
    const vec3 render_color = ACES_tone_map(average_color.rgb);
#endif
    imageStore(render_target, ivec2(gl_LaunchIDEXT.xy), vec4(render_color, 1.0));
}
